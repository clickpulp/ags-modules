// TERMS OF USE - CLICKPULP AGS MODULES (clickpulp-ags-modules)
//
// MIT License
//
// Copyright (c) 2024 Clickpulp, LLC
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.

#region Definitions

#define BUTTON_PRESSED_LOOPS 6
#define CURSOR_DEFAULT_RATE_OF_MOTION 8
#define CURSOR_RATE_OF_MOTION_FACTOR (Screen.Width / 160)

Point* _cursorClickTarget;
int _rateOfMotion;
int _buttonPressedTimer;
bool _cursorLocked;
bool _walkAxisMoving;
bool _arrowSelectEnabled;

#ifdef DEBUG
Overlay *_dbgOverlay;
DynamicSprite *_dbgSprite;
#endif

#endregion // Definitions

#region Public

bool get_ArrowSelectEnabled(static Cursor) {
  return _arrowSelectEnabled;
}

bool get_Locked(static Cursor) {
  return _cursorLocked;
}

bool get_HasClickTarget(static Cursor) {
  return _cursorClickTarget.x > 0 || _cursorClickTarget.y > 0;
}

static void Cursor::SetClickTarget(Point* clickTarget, bool force) {
  if (force || _cursorClickTarget.x != clickTarget.x || _cursorClickTarget.y != clickTarget.y) {
    _cursorClickTarget.x = clickTarget.x;
    _cursorClickTarget.y = clickTarget.y;
    mouse.SetRoomPosition(_cursorClickTarget.x, _cursorClickTarget.y);
  }
}

static void Cursor::ClearClickTarget() {
  _cursorClickTarget.x = 0;
  _cursorClickTarget.y = 0;
}

static void Cursor::SetLocked(bool locked) {
  _cursorLocked = locked;
}

static void Cursor::SetArrowSelectEnabled(bool enabled) {
  _arrowSelectEnabled = enabled;
}

static void Cursor::SetMousePositionToClickTarget() {
  if (!GUIStack.GUICount > 0 && !IsGamePaused() && Cursor.HasClickTarget) {
    Point *screenPoint = Screen.RoomToScreenPoint(_cursorClickTarget.x, _cursorClickTarget.y);
    Mouse.SetPosition(screenPoint.x, screenPoint.y);
  }
}

#endregion // Public

#region Private

float Lerpf(float from, float to, float t) {
  return from + (to - from) * t;
}

int _cleanup_axis_value(int value) {
  if (value > 0)
    return TweenMaths.ClampInt(value - INPUT_AXIS_DEADZONE, 0, (CONTROLLER_AXIS_RANGE_MAX * 90)/100 - INPUT_AXIS_DEADZONE);
  else if (value < 0)
    return TweenMaths.ClampInt(value + INPUT_AXIS_DEADZONE, (CONTROLLER_AXIS_RANGE_MIN * 90)/100 + INPUT_AXIS_DEADZONE, 0);
  return 0;
}

int _get_mouse_axis_delta(float value, float rateOfMotion) {
  readonly int result = FloatToInt(
    Maths.RaiseToPower(value, 2.0) * rateOfMotion,
    eRoundUp
  );
  
  if (value < 0.0) return -result;  
  return result;
}

void _update_buttonPressedTimer() {
  if (_buttonPressedTimer == 0) return;
  // TODO: check if axis/button has been released and pressed, and cancel just pressed?
  _buttonPressedTimer -= 1;
}

void _track_button_press() {
  if (_buttonPressedTimer > 0) return;
  _buttonPressedTimer = BUTTON_PRESSED_LOOPS; // TODO: Account for GetGameSpeed()
}

bool _is_button_pressed() {
  return _buttonPressedTimer > 0;
}

eKeyCode _get_character_direction_from_input() {
  if (inputUp.IsPressed()) {
    if (inputLeft.IsPressed()) return eDirectionUpLeft;
    if (inputRight.IsPressed()) return eDirectionUpRight;
    return eDirectionUp;
  }
  if (inputDown.IsPressed()) {
    if (inputLeft.IsPressed()) return eDirectionDownLeft;
    if (inputRight.IsPressed()) return eDirectionDownRight;
    return eDirectionDown;
  }
  if (inputLeft.IsPressed()) return eDirectionLeft;
  if (inputRight.IsPressed()) return eDirectionRight;
  
  return eDirectionNone;
}

void _update_arrow_select() {
  if (_arrowSelectEnabled && _walkAxisMoving && !_is_button_pressed()) {
    CharacterDirection direction = _get_character_direction_from_input();
    if (direction != eDirectionNone) {
      ArrowSelect.moveCursorDirection(direction);
      _track_button_press();
    }
  }
}

void _track_axis() {
  // This determine the priority of moving the mouse.
  // If the right stick is moving, move the mouse cursor
  // If not, the mouse will be controlled by other means
  if (axisCursorX.IsMoving() || axisCursorY.IsMoving()) {
    readonly float axisX = IntToFloat(_cleanup_axis_value(axisCursorX.Value));
    readonly float axisY = IntToFloat(_cleanup_axis_value(axisCursorY.Value));
    readonly float angle = Maths.ArcTan2(axisY, axisX);
    readonly float maxDistanceRange = IntToFloat(_cleanup_axis_value(CONTROLLER_AXIS_RANGE_MAX));
    
    readonly float distance = TweenMaths.MinFloat(Maths.Sqrt(
      Maths.RaiseToPower(axisX, 2.0) +
      Maths.RaiseToPower(axisY, 2.0)
    ), maxDistanceRange);
  
    readonly float maxDistance = Maths.Sqrt(Maths.RaiseToPower(maxDistanceRange, 2.0) * 2.0);
    readonly float distanceFactor = distance / maxDistance;
    readonly float rateOfMotion = IntToFloat(_rateOfMotion) * distanceFactor;
    
    int nextX = mouse.x;
    int nextY = mouse.y;
  #ifdef DEBUG
    int pX = nextX;
    int pY = nextY;
  #endif
    
    if (axisCursorX.IsMoving()) nextX += _get_mouse_axis_delta(Maths.Cos(angle) * distanceFactor, rateOfMotion);
    if (axisCursorY.IsMoving()) nextY += _get_mouse_axis_delta(Maths.Sin(angle) * distanceFactor, rateOfMotion);
   
    Cursor.SetClickTarget(Screen.ScreenToRoomPoint(nextX, nextY), true);
    Cursor.SetLocked(false);
    
  #ifdef DEBUG
    if (IsKeyPressed(eKeyC)) {
      DrawingSurface* surface = _dbgSprite.GetDrawingSurface();
      surface.DrawingColor = 14;
      surface.DrawLine(pX, pY, nextX, nextY);
      surface.DrawingColor = 16;
      surface.DrawLine(
        320, 240,
        320 + FloatToInt(Maths.Cos(angle) * distanceFactor * rateOfMotion * 10.0, eRoundNearest),
        240 + FloatToInt(Maths.Sin(angle) * distanceFactor * rateOfMotion * 10.0, eRoundNearest)
      );
      surface.Release();
    }
  #endif
  }
  
  if (!_walkAxisMoving && (axisLeftHorizontal.IsPressed || axisLeftVertical.IsPressed)) {
    _walkAxisMoving = true;
    Cursor.SetLocked(!axisCursorX.IsMoving() && !axisCursorY.IsMoving());
  }
  else if (_walkAxisMoving && axisLeftHorizontal.InDeadZone && axisLeftVertical.InDeadZone) { 
    _walkAxisMoving = false;
  }
}

#endregion // Private

#region Events

void game_start() {
  _cursorClickTarget = new Point;
  _rateOfMotion = CURSOR_DEFAULT_RATE_OF_MOTION * CURSOR_RATE_OF_MOTION_FACTOR;
  
  // Enable clickability only when inventory is cursor and player has use inv interaction
  player.Clickable = false;
  
#ifdef DEBUG
  _dbgSprite = DynamicSprite.Create(Screen.Width, Screen.Height, true);
  _dbgOverlay = Overlay.CreateGraphical(0, 0, _dbgSprite.Graphic, true);
  _dbgOverlay.ZOrder = 9999;
#endif
}

void on_event(EventType e, int data) {
  if (e == eEventEnterRoomBeforeFadein) {
    Cursor.SetLocked(true);
  }
}

void repeatedly_execute_always() {
  _update_buttonPressedTimer();
  
  if (Game.SkippingCutscene) {
    if (mouse.Visible) mouse.Visible = false;
    return;
  }
  
  readonly bool nextVisibility = !IsInBlockingScript() && (_arrowSelectEnabled || GUIStack.ShowingGUI || !Cursor.Locked || player.ActiveInventory != null || Dialog.IsDialogRunning());
  if (mouse.Visible != nextVisibility) mouse.Visible = nextVisibility;

  readonly bool updatePlayerClickable = player.ActiveInventory == null && player.Clickable || player.ActiveInventory != null && !player.Clickable;
  
  if (updatePlayerClickable && player.IsInteractionAvailable(eModeUseinv)) {
    readonly bool canInteractWithInvCursor = mouse.Visible && player.on && player.ActiveInventory != null;
    
    if (!player.Clickable && canInteractWithInvCursor) {
      player.Clickable = true;
    } else if (player.Clickable && !canInteractWithInvCursor) { 
      player.Clickable = false;
    }
  }
  
  if (!_arrowSelectEnabled && player.on && IsInterfaceEnabled() && !IsInBlockingScript() && !GUIStack.ShowingGUI && player.ActiveInventory == null) {
    if (Cursor.Locked && !Cursor.HasClickTarget) {
      readonly Point* p = Screen.RoomToScreenPoint(player.x, player.GetCenterY());
      Mouse.SetPosition(p.x, p.y);
    }
  }
  
  _track_axis();
  _update_arrow_select();
}

#endregion // Events

