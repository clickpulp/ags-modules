// TERMS OF USE - ROOM INDEX MODULE (ags-room-index-module)
//
// MIT License
//
// Copyright (c) 2024 Clickpulp, LLC
// Portions (c) 2018 Artium Nihamkin, artium@nihamkin.com
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.

#region Definitions

/** 
  * The the highest ID of a hotspot in the room. This is required because a 
  * static allocation for data in this module.
  * To disable hints on hotspots, set this to 0.
  */
#define MAX_ROOM_HOTSPOSTS_SUPPORTED 50

/** 
  * The the highest ID of a character in the game. This is required because a 
  * static allocation for data in this module.
  * To disable hints on charaters, set this to 0.
  */
#define MAX_CHARACTERS_SUPPORTED 5 // Update to your game's character count

/** 
  * The the highest ID of an object. This is required because a 
  * static allocation for data in this module.
  * To disable hints on objects, set this to 0.
  */
#define MAX_ROOM_OBJECTS_SUPPORTED 50

/*
 * IMPORTANT: This value should be the sum of 
 * MAX_ROOM_HOTSPOSTS_SUPPORTED,MAX_ROOM_OBJECTS_SUPPORTED,MAX_CHARACTERS_SUPPORTED
 */
#define TOTAL_HINTS_SUPPORTED 200

#endregion // Definitions

#region Variables

RoomEntity _entities[TOTAL_HINTS_SUPPORTED];
RoomEntity _scannedEntities[TOTAL_HINTS_SUPPORTED];
int _scanStartY = 0;
int _scanCountY = 0;

#endregion

#region RoomEntity

int Rect::get_Width() {
  return this.right - this.left;
}

int Rect::get_Height() {
  return this.bottom - this.top;
}


void RoomEntity::Update(int x, int y) {
  if (this.bounds == null) this.bounds = Rect.Create(Screen.Width, Screen.Height, 0, 0);

  if (this.bounds.bottom < y) this.bounds.bottom = y;
  if (this.bounds.top > y) this.bounds.top = y;
  if (this.bounds.left > x) this.bounds.left = x;
  if (this.bounds.right < x) this.bounds.right = x;
  
  this.initialised = true;
}

void RoomEntity::Clear() {
  this.bounds = null;
  this.initialised = false;
}

#endregion // _RoomElement

#region Private

bool _is_hotspot(int index) {
  return index < MAX_ROOM_HOTSPOSTS_SUPPORTED;
}

bool _is_character(int index) {
  return index >= MAX_ROOM_HOTSPOSTS_SUPPORTED && index < MAX_ROOM_HOTSPOSTS_SUPPORTED + MAX_CHARACTERS_SUPPORTED;
}

bool _is_object(int index) {
  return index >= MAX_ROOM_HOTSPOSTS_SUPPORTED + MAX_CHARACTERS_SUPPORTED && index < MAX_ROOM_HOTSPOSTS_SUPPORTED + MAX_CHARACTERS_SUPPORTED + MAX_ROOM_OBJECTS_SUPPORTED;
  
}

Hotspot* _get_hotspot(int index) {
  return hotspot[index];
}

Character* _get_character(int index) {
  return character[index - MAX_ROOM_HOTSPOSTS_SUPPORTED];
}

Object* _get_object(int index) {
  return object[index - (MAX_ROOM_HOTSPOSTS_SUPPORTED + MAX_CHARACTERS_SUPPORTED)];
}

void _clear_scanned_entities() {
  for (int i = 0; i < TOTAL_HINTS_SUPPORTED; i++) {
    _scannedEntities[i].Clear();
  }
}

void _clear_entities() {
  for (int i = 0; i < TOTAL_HINTS_SUPPORTED; i++) {
    _entities[i].Clear();
  }
  _scanCountY = 0;
}

void _flush_scanned_entities() {
  for (int i = 0; i < TOTAL_HINTS_SUPPORTED; i++) {
    _entities[i].bounds = _scannedEntities[i].bounds;
    _entities[i].initialised = _scannedEntities[i].initialised;
  }
  _scanStartY = 0;
}

void _scan_room_on_screen(int startY, int countY) {
  readonly String skipProp = "DisableInteraction";
  readonly String isExitProp = "exit";
  readonly int kInc = Screen.Width / 160;
  
  int x, y;  
  Hotspot *hs;
  Character *c;
  Object *obj;
 
  // Precalculate the bounding rectangle of each visible hotspot/object/character
  // Please notice that x,y are screen coordinares
  // We simply ignore stuff  that are not currently visible.
  // for (y = curr; y < curr + count; y += 4)
  for (y = startY; y < (startY + countY); y += kInc) {
    for (x = 0; x < Screen.Width; x += kInc) {      
      hs = Hotspot.GetAtScreenXY(x, y);
      if (
        hs != hotspot[0] && hs.Enabled && hs.ID < MAX_ROOM_HOTSPOSTS_SUPPORTED && !hs.GetProperty(skipProp) &&
        (hs.IsInteractionAvailable(eModeInteract) || hs.IsInteractionAvailable(eModeUseinv) || hs.GetProperty(isExitProp))
      ) {
        _scannedEntities[hs.ID].Update(x, y);
      }
      
      // NOTE: Any characters with the "Clickable" property set to false will not 
      // be seen by this function.
      c = Character.GetAtScreenXY(x, y);
      if (
        c != null && c != player && c.ID < MAX_CHARACTERS_SUPPORTED && c.Clickable &&
        (c.IsInteractionAvailable(eModeTalkto) || (!c.GetProperty(skipProp) && (c.IsInteractionAvailable(eModeInteract) || c.IsInteractionAvailable(eModeUseinv))))
      ) {
        _scannedEntities[c.ID + MAX_ROOM_HOTSPOSTS_SUPPORTED].Update(x, y);
      }
      
      obj = Object.GetAtScreenXY(x, y);
      if (
        obj != null && obj.Clickable && obj.ID < MAX_ROOM_OBJECTS_SUPPORTED && !obj.GetProperty(skipProp) &&
        (obj.IsInteractionAvailable(eModeInteract) || obj.IsInteractionAvailable(eModeUseinv))
      ) {
        _scannedEntities[obj.ID + MAX_ROOM_HOTSPOSTS_SUPPORTED + MAX_CHARACTERS_SUPPORTED].Update(x, y);
      }
    }
  }
}

void _scan_whole_room_on_screen() {
  _clear_scanned_entities();
  _scan_room_on_screen(0, Screen.Height);
  _flush_scanned_entities();
}

#endregion // Private

#region Events

void game_start() {
  if (
    TOTAL_HINTS_SUPPORTED !=
      MAX_ROOM_HOTSPOSTS_SUPPORTED + 
      MAX_ROOM_OBJECTS_SUPPORTED +
      MAX_CHARACTERS_SUPPORTED
  ) {
    AbortGame("Invalid value for TOTAL_HOTSPOTS_SUPPORTED");
  }
}

void repeatedly_execute_always() {
  if (GUIStack.GUICount > 0 || Game.SkippingCutscene)
    return;
  
  if (_scanCountY == 0) _scanCountY = Screen.Height / GetGameSpeed();
  
  if (_scanStartY + _scanCountY > Screen.Height) {
    _flush_scanned_entities();
    _clear_scanned_entities();
  } else {  
    _scan_room_on_screen(_scanCountY, _scanCountY);
    _scanStartY += _scanCountY;
  }
}

void on_event(EventType type, int data) {
  switch (type) {
    case eEventEnterRoomBeforeFadein:      
      _scan_whole_room_on_screen();
      break;
      
    case eEventLeaveRoomAfterFadeout:
      _clear_entities();
      break;
      
    case eEventRestoreGame:
      _scan_whole_room_on_screen();
      break;
  }
}

#endregion // Events
