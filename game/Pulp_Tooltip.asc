#define TOOLTIP_MARGIN 8
#define TOOLTIP_COLOR 16
#define TIMER_NUMBER 7

Overlay* _overlay;

void Tooltip::Remove() {
  if (_overlay != null && _overlay.Valid) {
    _overlay.StopAllTweens();
    _overlay.Remove();
    _overlay = null;
    PatchValues.SetInt("tooltip_hidden", 0);
  }
}

static Tooltip* Tooltip::Create(int screenX, int screenY, const string text) {
  bool isVisible;
  int width = Screen.Width * 7/12 - TOOLTIP_MARGIN*2;
  if (_overlay != null) {
    isVisible = true;
    _overlay.SetText(width, pulpGlobals.TooltipFont, TOOLTIP_COLOR, text);
  }
  else {
    _overlay = Overlay.CreateTextual(0, 0, width, pulpGlobals.TooltipFont, TOOLTIP_COLOR, text);
    _overlay.ZOrder = 32767;
  }
  
  int yMarginTop = TOOLTIP_MARGIN;
  
  if (pulpGlobals.InventoryGUI != null && pulpGlobals.InventoryGUI.IsInStack()) {
    // Assumes that the inventory GUI is a top bar
    yMarginTop += pulpGlobals.InventoryGUI.Height;
  }
  
  _overlay.X = TweenMaths.ClampInt(screenX - _overlay.Width/2, TOOLTIP_MARGIN, Screen.Width - TOOLTIP_MARGIN - _overlay.Width);
  _overlay.Y = TweenMaths.ClampInt(screenY - _overlay.Height/2, yMarginTop, Screen.Height - TOOLTIP_MARGIN*2 - _overlay.Height);
  
  if (GUIStack.TopGUI != null) _overlay.ZOrder = GUIStack.TopGUI.ZOrder - 1;
  else _overlay.ZOrder = 0;
  if (isVisible) {
    _overlay.Transparency = 0;
  }
  else {
    _overlay.Transparency = 100;
    _overlay.TweenFadeIn(0.25, eEaseLinearTween, eNoBlockTween);
  }
  
  return new Tooltip;
}

void repeatedly_execute_always() {
  if (_overlay == null) return;
  
  bool hide = Dialog.IsDialogRunning() || IsGamePaused() || IsInBlockingScript() || Game.InSkippableCutscene;
  if (_overlay.Transparency == 0 && hide && !PatchValues.GetInt("tooltip_hidden", 0)) {
    _overlay.StopAllTweens();
    _overlay.TweenFadeOut(0.25, eEaseLinearTween, eNoBlockTween);
    PatchValues.SetInt("tooltip_hidden", 1);
  }
  else if (_overlay.Transparency == 100 && !hide && PatchValues.GetInt("tooltip_hidden", 0)) {
    _overlay.StopAllTweens();
    _overlay.TweenFadeIn(0.25, eEaseLinearTween, eNoBlockTween);
    PatchValues.SetInt("tooltip_hidden", 0);
  }
}
